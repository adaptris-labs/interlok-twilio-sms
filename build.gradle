import org.apache.tools.ant.taskdefs.condition.Os
import org.apache.tools.ant.filters.*
plugins {
  id "java"
  id "distribution"
  id "jacoco"
  id "com.palantir.docker" version "0.25.0"
  id 'com.github.spotbugs' version '4.4.4'
  id "org.sonarqube" version "3.0"
}


ext {
  interlokParentGradle = 'https://raw.githubusercontent.com/adaptris-labs/interlok-build-parent/master/build.gradle'
  interlokVersion = '3.10.2-RELEASE'
  interlokUiVersion = interlokVersion
  releaseVersion = '3.10-SNAPSHOT'
  nexusBaseUrl  = 'https://nexus.adaptris.net'
  javadocsBaseUrl = "${nexusBaseUrl}/nexus/content/sites/javadocs/com/adaptris"
  interlokJavadocs= project.hasProperty('interlokJavadocs') ? project.getProperty('interlokJavadocs') : javadocsBaseUrl + "/interlok-core/" + interlokVersion
  interlokCommonJavadocs= project.hasProperty('interlokJavadocs') ? project.getProperty('interlokJavadocs') : javadocsBaseUrl + "/interlok-common/" + interlokVersion
  componentName='Interlok/Twilio SMS'
  organizationName = "Adaptris Labs"
  organizationUrl = "https://interlok.adaptris.net/"
  slf4jVersion = '1.7.30'
  // Custom properties to make verify work, since these are not checked in
  interlokVerifySystemProperties = [ 'twilio.account.sid': 'Dummy', 'twilio.account.auth': 'Dummy', 'twilio.sms.from': '+4412345678', 'twilio.sms.to': '+4412345678' ]
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}

allprojects {
  apply from: "${interlokParentGradle}"
}


sourceCompatibility = 1.8
group   = 'io.github.adaptris-labs'
version = releaseVersion
def versionDir = "$buildDir/version"

dockerfileZip.enabled=false

repositories {
    mavenCentral()
    maven { url "${nexusBaseUrl}/nexus/content/groups/public" }
    maven { url "${nexusBaseUrl}/nexus/content/groups/interlok" }
}

configurations {
  javadoc {}
  // patch interlokRuntime to extend runtime, so we only declare our dependencies once.
  interlokRuntime.extendsFrom runtime
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

dependencies {
  compile ("com.adaptris:interlok-core:$interlokVersion") { changing= true}
  compile ("com.adaptris:interlok-common:$interlokVersion") { changing= true}
  compile ("org.slf4j:slf4j-api:$slf4jVersion")
  compile ("com.twilio.sdk:twilio:7.11+") {
    exclude group:"org.apache.httpcomponents", module: "httpclient"
    exclude group:"com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group:"com.fasterxml.jackson.core", module: "jackson-core"
  }
  compile ("org.apache.httpcomponents:httpclient:4.5.12")
  compile ("com.fasterxml.jackson.core:jackson-databind:2.11.1")

  testImplementation ('junit:junit:4.13')
  testImplementation ("com.adaptris:interlok-stubs:$interlokVersion") {
    changing= true
    exclude group: "org.apache.logging.log4j"
  }
  testImplementation ("org.slf4j:slf4j-simple:$slf4jVersion")

  // This is to make sure that the annotation processors are enabled during the
  // compile phase.
  annotationProcessor ("com.adaptris:interlok-core-apt:$interlokVersion") { changing= true}
  testAnnotationProcessor ("com.adaptris:interlok-core-apt:$interlokVersion") { changing= true}
  // To handle the custom javadoc taglets.
  javadoc("com.adaptris:interlok-core-apt:$interlokVersion") { changing= true}

  // patch interlokRuntime to include the custom jar.
  interlokRuntime files(jar.archiveFile){
    builtBy 'jar'
  }
}

jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName)
  }
}

jacocoTestReport {
  reports {
    xml.enabled true
    html.enabled true
  }
}
check.dependsOn jacocoTestReport

spotbugsMain {
  effort = "max"
  reportLevel = "high"
  // showProgress = "true"
  effort = "max"
  // Ignore failures?
  // ignoreFailures = false
  reports {
    xml.enabled = true
    html.enabled = false
  }

  //includeFilter = new File("$rootDir/gradle/spotbugs-filter.xml")
  //excludeFilter = new File("$rootDir/gradle/spotbugs-exclude.xml")
}

// disable spotbugsTests which checks our test code..
spotbugsTest.enabled = false

// Generate the META-INF/adaptris-version file since we want to have it reported in the UI.
sourceSets {
  main {
    output.dir(versionDir, builtBy: 'generateVersion')
  }
}

task generateVersion {
  doLast {
    def versionFile = new File(new File(versionDir, 'META-INF'), 'adaptris-version')
    versionFile.getParentFile().mkdirs()
    ant.propertyfile(file: versionFile) {
      entry(key: 'component.name', value: componentName)
      entry(key: 'build.version', value: releaseVersion)
      entry(key: 'groupId', value: project.group)
      entry(key: 'artifactId', value: project.name)
      entry(key: 'build.date', value: new Date().format('yyyy-MM-dd'))
      entry(key: 'build.info', value:  new Date().format('HH:mm:ss z'))
    }
  }
}

// Since we have custom code in play, we have to "patch" the verifyLauncherJar + serviceTesterLauncher configurations to depend on jar.
verifyLauncherJar.configure {
    dependsOn jar
}

serviceTesterLauncherJar.configure {
    dependsOn jar
}

docker {
  name "adaptrislabs/twilio-sms"
  tags 'latest'
  pull true
  files tasks.installDist.outputs
}

docker.dependsOn installDist
assemble.dependsOn installDist
installDist.dependsOn jar

sonarqube {
  properties {
    property "sonar.projectKey", "adaptris-labs_interlok-twilio-sms"
    property "sonar.organization", "adaptris-labs"
    property "sonar.host.url", "https://sonarcloud.io"
    property "sonar.sourceEncoding", "UTF-8"
    property "sonar.sources", "./src/main/interlok,./src/main/java"
    property "sonar.tests", "./src/test/java"
    property "sonar.coverage.jacoco.xmlReportPaths", "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    property "sonar.java.spotbugs.reportPaths", "${buildDir}/reports/spotbugs/main.xml"
    property "sonar.externalIssuesReportPaths", interlokVerifySonarReport
  }
}

project.tasks["sonarqube"].dependsOn check
