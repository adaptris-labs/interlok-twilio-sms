import org.apache.tools.ant.taskdefs.condition.Os

plugins {
  id "java"
  id "distribution"
  id "maven"
  id "maven-publish"
  id "jacoco"
  id "com.palantir.docker" version "0.19.2"
}

ext {
  interlokCoreVersion = project.hasProperty('interlokCoreVersion') ? project.getProperty('interlokCoreVersion') : '3.8-SNAPSHOT'
  releaseVersion = project.hasProperty('releaseVersion') ? project.getProperty('releaseVersion') : '3.8-SNAPSHOT'
  mavenPublishUrl = project.hasProperty('mavenPublishUrl') ? project.getProperty('mavenPublishUrl') : 'https://development.adaptris.net/nexus/content/repositories/snapshots'
  repoUsername = project.hasProperty('repoUsername') ? project.getProperty('repoUsername') : 'unknown'
  repoPassword = project.hasProperty('repoPassword') ? project.getProperty('repoPassword') : 'unknown'
  defaultNexusRepo = project.hasProperty('defaultNexusRepo') ? project.getProperty('defaultNexusRepo') : 'https://repo1.maven.org/maven2/'
  javadocsBaseUrl = "https://development.adaptris.net/nexus/content/sites/javadocs/com/adaptris"
  interlokJavadocs= project.hasProperty('interlokJavadocs') ? project.getProperty('interlokJavadocs') : javadocsBaseUrl + "/interlok-core/" + interlokCoreVersion
  interlokCommonJavadocs= project.hasProperty('interlokJavadocs') ? project.getProperty('interlokJavadocs') : javadocsBaseUrl + "/interlok-common/" + interlokCoreVersion
  componentName='Interlok/Twilio SMS'
  organizationName = "Quotidian Ennui"
  organizationUrl = "http://quotidian-ennui.github.io"
  executableClassifier = Os.isFamily(Os.FAMILY_WINDOWS) ? "win64" : "unix"
}

if (JavaVersion.current().isJava8Compatible()) {
  allprojects {
    tasks.withType(Javadoc) {
      options.addStringOption('Xdoclint:none', '-quiet')
    }
  }
}


sourceCompatibility = 1.8
group   = 'io.github.quotidian-ennui'
version = releaseVersion
def versionDir = "$buildDir/version"


distTar.enabled=false
distZip.enabled=false
dockerfileZip.enabled=false

repositories {
  jcenter()
  maven { url "https://repo1.maven.org/maven2/" }
  maven { url "https://development.adaptris.net/nexus/content/groups/public" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/snapshots" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/releases" }
}


configurations {
  javadoc {}
  war{}
  interlokRuntime{}
  interlokBinary{}
  all*.exclude group: 'c3p0'
  all*.exclude group: 'commons-logging'
  all*.exclude group: 'javamail'
  all*.exclude group: 'javax.mail', module: 'mail'
  all*.exclude group: 'org.glassfish.hk2.external'
  all*.exclude group: 'xalan', module: 'xalan'
  all*.exclude group: 'net.sf.saxon', module: 'saxon'
  all*.exclude group: 'org.codehaus.woodstox'
  all*.exclude group: 'org.eclipse.jetty.orbit', module: 'javax.mail.glassfish'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, "seconds"
}

dependencies {
  compile ("com.adaptris:interlok-core:$interlokCoreVersion") { changing= true}
  compile ("com.adaptris:interlok-common:$interlokCoreVersion") { changing= true}
  compile ("org.slf4j:slf4j-api:1.7.25")
  compile ("com.twilio.sdk:twilio:7.11+") {
    exclude group:"org.apache.httpcomponents", module: "httpclient"
    exclude group:"com.fasterxml.jackson.core", module: "jackson-databind"
    exclude group:"com.fasterxml.jackson.core", module: "jackson-core"
  }
  compile ("org.apache.httpcomponents:httpclient:4.5.6")
  compile ("com.fasterxml.jackson.core:jackson-databind:2.9.6")
  interlokRuntime ("com.adaptris:interlok-varsub:$interlokCoreVersion") { changing=true }
  interlokRuntime ("com.adaptris:interlok-xinclude:$interlokCoreVersion") { changing=true }
  // Doesn't exist yet...
  // interlokBinary group: "com.adaptris", name:"interlok-executable", version:"1.0.0", classifier: executableClassifier
  testCompile ('junit:junit:4.12')
  testCompile ("com.adaptris:interlok-stubs:$interlokCoreVersion") { changing= true}
  testCompile ("org.apache.logging.log4j:log4j-core:2.11.1")
  testCompile ("org.apache.logging.log4j:log4j-1.2-api:2.11.1")
  testCompile ("org.apache.logging.log4j:log4j-slf4j-impl:2.11.1")
  testCompile ("org.apache.logging.log4j:log4j-api:2.11.1")

  javadoc("com.adaptris:interlok-core-apt:$interlokCoreVersion") { changing= true}
  war ("com.adaptris.ui:interlok:$interlokCoreVersion@war") {changing=true}
}


jar {
  manifest {
    attributes("Built-By": System.getProperty('user.name'),
               "Build-Jdk": System.getProperty('java.version'),
               "Implementation-Title": componentName,
               "Implementation-Version": project.version,
               "Implementation-Vendor-Id": project.group,
               "Implementation-Vendor": organizationName)
  }
}


sourceSets {
  main {
    output.dir(versionDir, builtBy: 'generateVersion')
  }
}
// Generate the META-INF/adaptris-version file
task generateVersion {
  doLast {
    def versionFile = new File(new File(versionDir, 'META-INF'), 'adaptris-version')
    versionFile.getParentFile().mkdirs()
    ant.propertyfile(file: versionFile) {
      entry(key: 'component.name', value: componentName)
      entry(key: 'build.version', value: releaseVersion)
      entry(key: 'groupId', value: project.group)
      entry(key: 'artifactId', value: project.name)
      entry(key: 'build.date', value: new Date().format('yyyy-MM-dd'))
    }
  }
}

javadoc {
  configure(options) {
      links = [
        interlokJavadocs,
        interlokCommonJavadocs,
      ]
    taglets = ["com.adaptris.taglet.ConfigTaglet", "com.adaptris.taglet.LicenseTaglet"]
    options.addStringOption "tagletpath", configurations.javadoc.asPath
  }
}

jacoco {
  toolVersion="0.8.1"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task examplesJar(type: Jar, dependsOn: test) {
  classifier = 'examples'
  from new File(buildDir, '/examples')
}

artifacts {
  archives javadocJar
  archives examplesJar
}


distributions {
  main {
    contents {
      from {
        "$projectDir/src/main/interlok"
      }
      into('lib') {
        from(project.configurations.runtime)
        from(project.configurations.interlokRuntime)
        from jar
      }
      into('bin') {
        from(project.configurations.interlokBinary)
      }
      into('webapps') {
        from(project.configurations.war)
      }
      rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
      rename '(.*)-[0-9]+\\..*.war', '$1.war'
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java

      artifact javadocJar { classifier "javadoc" }
      artifact examplesJar { classifier "examples" }

      pom.withXml {
        asNode().appendNode("description", "Send SMS via your Twilio account")
        def properties = asNode().appendNode("properties")
        properties.appendNode("target", "3.6.6+")
      }
    }
  }
  repositories {
    maven {
      credentials {
        username repoUsername
        password repoPassword
      }
      url mavenPublishUrl
    }
  }
}


docker {
  name "lewinc/twilio-sms"
  tags 'latest'
  pull true
  files tasks.installDist.outputs
}

docker.dependsOn installDist
check.dependsOn jacocoTestReport